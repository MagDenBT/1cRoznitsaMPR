
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет настройку интеграции с платежными системами на основании
// данных продажи / возврата
//
// Параметры:
//  Организация - ОпределяемыйТип.ОрганизацияРМК - организация из документа продажи;
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектРМК - касса из документа продажи;
//  ИдентификаторПлатежнойСистемы - строка - идентификатор платежной системы из документа продажи.
//  ПлатежнаяСистемаККТ - ПеречислениеСсылка.ТипыПлатежнойСистемыККТ - вид оплаты из документа продажи.
//
// Возвращаемое значение:
//  ТаблицаЗначений - настройки интеграции с платежной системой.
//
Функция НастройкиИнтеграции(Организация, ТорговыйОбъект, ИдентификаторПлатежнойСистемы = Неопределено, ПлатежнаяСистемаККТ = Неопределено) Экспорт
	
	Если НЕ ИнтеграцияСПлатежнымиСистемами.ИнтеграцияДоступна() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса = ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ТекстЗапросаНастройкиИнтеграции(ИдентификаторПлатежнойСистемы);
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Организация", 					Организация);
	Запрос.УстановитьПараметр("ТорговыйОбъект", 				ТорговыйОбъект);
	Запрос.УстановитьПараметр("ИдентификаторПлатежнойСистемы", 	ИдентификаторПлатежнойСистемы);
	Запрос.УстановитьПараметр("ПлатежнаяСистемаККТ", 			ПлатежнаяСистемаККТ);
	
	РезультатЗапроса = Запрос.Выполнить();
	Интеграции 		 = РезультатЗапроса.Выгрузить();
	
	Если Интеграции.Количество() > 0 Тогда
		Возврат Интеграции;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
