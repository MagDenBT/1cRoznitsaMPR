#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Организации") Тогда
		
		Наименование           = ДанныеЗаполнения.Наименование;
		ЮрФизЛицо              = ДанныеЗаполнения.ЮрФизЛицо;
		НаименованиеПолное     = ДанныеЗаполнения.НаименованиеПолное;
		ОсновнойБанковскийСчет = ДанныеЗаполнения.ОсновнойБанковскийСчет;
		ИНН                    = ДанныеЗаполнения.ИНН;
		КПП                    = ДанныеЗаполнения.КПП;
		КодПоОКПО              = ДанныеЗаполнения.КодПоОКПО;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Наименование") Тогда
			Наименование = ДанныеЗаполнения.Наименование;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("НаименованиеПолное") Тогда
			НаименованиеПолное = ДанныеЗаполнения.НаименованиеПолное;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ИНН") Тогда
			ИНН = ДанныеЗаполнения.ИНН;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("КПП") Тогда
			КПП = ДанныеЗаполнения.КПП;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбщегоНазначенияРТ.УпрощенныйВводДоступен() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	ЭтоЮрЛицо = ЭтоЮрЛицо(ЮрФизЛицо);
	
	Если (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо
		ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель) 
		//{ds-29.10.2020
		И Не РольДоступна("ПолныеПрава")
		//}
		Тогда
		
		Если НЕ ПустаяСтрока(ИНН) Тогда
			Если НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, ЭтоЮрЛицо, ТекстСообщения) Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН",, Отказ);
			КонецЕсли;
		Иначе
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен ИНН'"), ЭтотОбъект, "ИНН",, Отказ);
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(КодПоОКПО)
			И НЕ РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(КодПоОКПО, ЭтоЮрЛицо, ТекстСообщения) Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "КодПоОкпо",, Отказ);
		КонецЕсли;
		
		Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			Если УчастникЭДО И Не ЗначениеЗаполнено(СокрЛП(НаименованиеПолное)) Тогда
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указано полное наименование'"), ЭтотОбъект, "НаименованиеПолное",, Отказ);
			КонецЕсли;
			Если НЕ ПустаяСтрока(КПП) Тогда
				Если НЕ РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, ТекстСообщения) Тогда
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "КПП",, Отказ);
				КонецЕсли;
			Иначе
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен КПП'"), ЭтотОбъект, "КПП",, Отказ);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		Если Не ЗначениеЗаполнено(ФизЛицо) Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указано физ. лицо'"), ЭтотОбъект, "ФизЛицо",, Отказ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		ЗакупкиСервер.ОтразитьПоставщикаТоваровКакДействующего(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо 
		И ЗначениеЗаполнено(ФизЛицо) Тогда
		КонтактнаяИнформация.Очистить();
		ИНН = "";
	ИначеЕсли Не ЭтоГруппа Тогда
		ФизЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// По переданному значению перечисления ЮрФизЛицо определяет, является ли оно признаком ЮрЛица.
//
// Параметры:
//  ЮрФизЛицо  -   Перечисления.ЮрФизЛицо.
// Возвращаемое значение:
//   Булево   - Истина, если юридическое лицо, Ложь если нет.
Функция ЭтоЮрЛицо(ЮрФизЛицо) 

	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		Возврат Истина;
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо 
			ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

#КонецОбласти

#КонецЕсли
