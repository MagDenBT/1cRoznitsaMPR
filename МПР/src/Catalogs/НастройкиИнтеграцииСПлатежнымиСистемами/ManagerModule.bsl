///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Определяет параметры копирования торговой точки.
//
// Параметры:
//  ТорговаяТочка - СправочникСсылка.НастройкиИнтеграцииСПлатежнымиСистемами - настройка выполнения оплаты.
//
// Возвращаемое значение:
//  Структура - параметры копирования:
//    * Наименование - Строка - наименование копируемой торговой точки;
//    * СверкаВзаиморасчетов - Булево - признак выполнения сверки взаиморасчетов;
//    * ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//        для которой производится настройка.
//
Функция ДанныеКопирования(ТорговаяТочка) Экспорт
	
	ДанныеКопирования = Новый Структура;
	ДанныеКопирования.Вставить("Наименование", "");
	ДанныеКопирования.Вставить("СверкаВзаиморасчетов", Ложь);
	ДанныеКопирования.Вставить("ПлатежнаяСистема", Неопределено);
	
	Если ТорговаяТочка = Неопределено Тогда
		Возврат ДанныеКопирования;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиИнтеграцииСПлатежнымиСистемами.СверкаВзаиморасчетов КАК СверкаВзаиморасчетов,
		|	НастройкиИнтеграцииСПлатежнымиСистемами.Наименование КАК Наименование,
		|	НастройкиИнтеграцииСПлатежнымиСистемами.Родитель.ПлатежнаяСистема КАК ПлатежнаяСистема
		|ИЗ
		|	Справочник.НастройкиИнтеграцииСПлатежнымиСистемами КАК НастройкиИнтеграцииСПлатежнымиСистемами
		|ГДЕ
		|	НастройкиИнтеграцииСПлатежнымиСистемами.Ссылка = &ТорговаяТочка";
	
	Запрос.УстановитьПараметр("ТорговаяТочка", ТорговаяТочка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ЗаполнитьЗначенияСвойств(
		ДанныеКопирования,
		ВыборкаДетальныеЗаписи,
		"СверкаВзаиморасчетов, Наименование, ПлатежнаяСистема");
	
	Возврат ДанныеКопирования;
	
КонецФункции

// Определяет наличие настроек интеграции с Системой быстрых платежей.
//
// Возвращаемое значение:
//  Булево - если Истина, есть настройки подключения.
//
Функция ЕстьНастройкиПодключения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Признак
		|ИЗ
		|	Справочник.НастройкиИнтеграцииСПлатежнымиСистемами КАК НастройкиИнтеграцииСПлатежнымиСистемами
		|ГДЕ
		|	НЕ НастройкиИнтеграцииСПлатежнымиСистемами.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Определяет настройки интеграции с Системой быстрых платежей
// по участнику.
//
// Параметры:
//  УчастникСБП - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//    для которой выполняется настройка.
//
// Возвращаемое значение:
//  Массив Из НастройкиИнтеграцииСПлатежнымиСистемами - используемые настройки.
//
Функция НастройкиПодключенияУчастникаСБП(УчастникСБП) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиИнтеграцииСПлатежнымиСистемами.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиИнтеграцииСПлатежнымиСистемами КАК НастройкиИнтеграцииСПлатежнымиСистемами
		|ГДЕ
		|	НастройкиИнтеграцииСПлатежнымиСистемами.Родитель.ПлатежнаяСистема = &УчастникСБП
		|	И НастройкиИнтеграцииСПлатежнымиСистемами.Используется";
	
	Запрос.УстановитьПараметр("УчастникСБП", УчастникСБП);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет настройки подключения к СБП в информационную базу.
//
// Параметры:
//  ПараметрыЗаполнения - Структура - данные заполнения торговой точки;
//  ПараметрыАутентификации - Структура - данные аутентификации в платежной системе;
//  ПараметрыОплаты - Структура - содержит данные для записи настроек в регистр сведений.
//    Структура параметра соответствует структуре регистра, которая определена
//    в метаданных за исключением полей указанных в настройках в свойстве ИсключаемыеПоля
//    процедуры ПриОпределенииНастроекИнтеграции;
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система, для которой производится настройка.
//
// Возвращаемое значение:
//  Структура - результат создания торговой точки:
//    * Ошибка - Булево - признак ошибки сохранения;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//
Функция НоваяТорговаяТочка(
		ПараметрыЗаполнения,
		ПараметрыАутентификации,
		ПараметрыОплаты,
		ПлатежнаяСистема) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	
	ИдентификаторТорговойТочки = СервисИнтеграцииССБП.ИдентификаторТорговойТочки(
		ПлатежнаяСистема,
		ПараметрыАутентификации);
	
	РодительНастройки = РодительНастройки(ПлатежнаяСистема);
	
	НачатьТранзакцию();
	
	Попытка
		
		ТорговаяТочка = Справочники.НастройкиИнтеграцииСПлатежнымиСистемами.СоздатьЭлемент();
		ТорговаяТочка.ТорговаяТочка = ИдентификаторТорговойТочки;
		ТорговаяТочка.Наименование = ПараметрыЗаполнения.Наименование;
		ТорговаяТочка.СверкаВзаиморасчетов = ПараметрыЗаполнения.СверкаВзаиморасчетов;
		ТорговаяТочка.Родитель = РодительНастройки;
		ТорговаяТочка.Используется = Истина;
		
		ТорговаяТочка.Записать();
		
		РезультатЗаписиНастроек = ИнтеграцияСПлатежнымиСистемамиСлужебный.ЗаписатьНастройкиОплаты(
			ПараметрыОплаты,
			ТорговаяТочка.Ссылка);
		
		Если РезультатЗаписиНастроек.Отказ Тогда
			Результат.Ошибка = Истина;
			Результат.СообщениеОбОшибке = РезультатЗаписиНастроек.СообщениеОбОшибке;
			ОтменитьТранзакцию();
			Возврат Результат;
		КонецЕсли;
		
		ИнтеграцияСПлатежнымиСистемамиСлужебный.СохранитьНастройкиАутентификации(
			ТорговаяТочка.Ссылка,
			ПараметрыАутентификации,
			ПлатежнаяСистема);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Результат.Ошибка = Истина;
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать торговую точку мерчанта по причине:
				|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Создает корневой элемент настроек подключения к СБП, если он отсутствует.
//
// Параметры:
//  ПлатежнаяСистема - ПеречислениеСсылка.ПлатежныеСистемы - платежная система,
//    для которой выполняется настройка.
//
// Возвращаемое значение:
//  СправочникСсылка.НастройкиИнтеграцииСПлатежнымиСистемами - новый или найденный корневой элемент.
//
Функция РодительНастройки(ПлатежнаяСистема)
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиИнтеграцииСПлатежнымиСистемами");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	НастройкиИнтеграцииСПлатежнымиСистемами.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.НастройкиИнтеграцииСПлатежнымиСистемами КАК НастройкиИнтеграцииСПлатежнымиСистемами
			|ГДЕ
			|	НастройкиИнтеграцииСПлатежнымиСистемами.ПлатежнаяСистема = &ПлатежнаяСистема";
		
		Запрос.УстановитьПараметр("ПлатежнаяСистема", ПлатежнаяСистема);
		
		РезультатЗапроса = Запрос.Выполнить();
		УчетнаяЗаписьСсылка = Неопределено;
		Если РезультатЗапроса.Пустой() Тогда
			РодительНастройки = СоздатьГруппу();
			РодительНастройки.ПлатежнаяСистема = ПлатежнаяСистема;
			РодительНастройки.Наименование     = ИнтеграцияСПлатежнымиСистемамиСлужебный.СинонимЗначенияПеречисления(ПлатежнаяСистема);
			РодительНастройки.Идентификатор    = ИнтеграцияСПлатежнымиСистемамиСлужебный.ИдентификаторСБП();
			РодительНастройки.Записать();
			
			РодительНастройкиСсылка = РодительНастройки.Ссылка;
		Иначе
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			РодительНастройкиСсылка = ВыборкаДетальныеЗаписи.Ссылка;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать нового родителя настройки:
				|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Возврат РодительНастройкиСсылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли