
#Область ПрограммныйИнтерфейс

// Получает массив GTIN для переданного товара и характеристики
//
// Параметры:
//  Номенклатура   - СправочникСсылка.Номенклатура - номенклатура (маркируемый товар).
//  Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика номенклатуры (маркируемого товара).
// 
// Возвращаемое значение:
//  Массив - массив GTIN
//
Функция МассивGTINМаркированногоТовара(Номенклатура, Характеристика) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Владелец = &Номенклатура
	|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика");
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	МассивШтрихкодов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Штрихкод");
	СписокGTIN =  Новый Массив;
	
	Для Каждого Штрихкод Из МассивШтрихкодов Цикл
		
		Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(Штрихкод) Тогда
			СписокGTIN.Добавить(Штрихкод);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокGTIN;
	
КонецФункции

// Определяет ОрганизациюЕГАИС в соответствии с настройкой распределния продаж.
// 
// Параметры:
//  Магазин - СправочникСсылка.Магазины.
//  РабочееМесто - СправочникСсылка.РабочиеМеста.
//  Номенклатура - СправочникСсылка.Номенклатура.
//  ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС.
//
Процедура ПереопределитьОрганизациюЕГАИС(Магазин, РабочееМесто, Номенклатура, ОрганизацияЕГАИС) Экспорт
	
	ИнтеграцияИСРТ.ПереопределитьОрганизациюЕГАИС(Магазин, РабочееМесто, Номенклатура, ОрганизацияЕГАИС);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

Функция ВидыПродукцииВТоварах(Знач Товары) Экспорт
	
	Возврат ИнтеграцияИСРТ.ВидыПродукцииВТоварах(Товары);
	
КонецФункции

Функция ЭтоРозничныйКлиент(Контрагент) Экспорт
	
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ЮрФизЛицо");
	Возврат ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо");
	
КонецФункции

#КонецОбласти