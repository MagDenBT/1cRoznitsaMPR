#Область ПрограммныйИнтерфейс

Процедура ДобавитьТекстЗапросаОбъектыПоТелефону(МассивЗапросов, ФильтрПоискаПоШтрихкоду) Экспорт
	
	СУсловиемПоиска = ФильтрПоискаПоШтрихкоду.Количество() > 0;
	
	МассивСправочниковСКИ = Новый Массив;
	РаботаСоШтрихкодамиПереопределяемый.ЗаполнитьМассивСправочниковСКИ(МассивСправочниковСКИ, ФильтрПоискаПоШтрихкоду);
	
	Для Каждого СтрокаМассива Из МассивСправочниковСКИ Цикл
		
		Если СУсловиемПоиска И НЕ ФильтрПоискаПоШтрихкоду.Свойство(СтрокаМассива) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстЗапросаКИ = "ВЫБРАТЬ
		|	КонтактнаяИнформация.НомерТелефона,
		|	КонтактнаяИнформация.Ссылка,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ЗНАЧЕНИЕ(Перечисление.ШаблоныШтрихкодов.Телефон)
		|ИЗ
		|	&ТаблицаКИ КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Тип = Значение(Перечисление.ТипыКонтактнойИнформации.Телефон) И &Условие";
		
		ТекстЗапросаКИ = СтрЗаменить(ТекстЗапросаКИ, "&ТаблицаКИ" , "Справочник." + СтрокаМассива + ".КонтактнаяИнформация");
		ТекстЗапросаКИ = СтрЗаменить(ТекстЗапросаКИ, "И &Условие", 
			?(СУсловиемПоиска, "И КонтактнаяИнформация.НомерТелефона = &НомерТелефона", ""));
		МассивЗапросов.Добавить(ТекстЗапросаКИ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаОбъектыПоАдресуЭП(МассивЗапросов, ФильтрПоискаПоШтрихкоду) Экспорт
	
	СУсловиемПоиска = ФильтрПоискаПоШтрихкоду.Количество() > 0;

	МассивСправочниковСКИ = Новый Массив;
	РаботаСоШтрихкодамиПереопределяемый.ЗаполнитьМассивСправочниковСКИ(МассивСправочниковСКИ, ФильтрПоискаПоШтрихкоду);
	
	Для Каждого СтрокаМассива Из МассивСправочниковСКИ Цикл
		
		Если СУсловиемПоиска И НЕ ФильтрПоискаПоШтрихкоду.Свойство(СтрокаМассива) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстЗапросаКИ = 
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.Представление,
		|	КонтактнаяИнформация.Ссылка,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ЗНАЧЕНИЕ(Перечисление.ШаблоныШтрихкодов.АдресЭП)
		|ИЗ
		|	&ТаблицаКИ КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Тип = Значение(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты) И &Условие";
		
		ТекстЗапросаКИ = СтрЗаменить(ТекстЗапросаКИ, "&ТаблицаКИ" , "Справочник." + СтрокаМассива + ".КонтактнаяИнформация");
		ТекстЗапросаКИ = СтрЗаменить(ТекстЗапросаКИ, "И &Условие", 
			?(СУсловиемПоиска, "И КонтактнаяИнформация.АдресЭП = &АдресЭП", ""));
		МассивЗапросов.Добавить(ТекстЗапросаКИ);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаТелефонПоШаблону() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НомерТелефона,
	|	НЕОПРЕДЕЛЕНО,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ЗНАЧЕНИЕ(Перечисление.ШаблоныШтрихкодов.Телефон)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаАдресЭППоШаблону() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&АдресЭП,
	|	НЕОПРЕДЕЛЕНО,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ЗНАЧЕНИЕ(Перечисление.ШаблоныШтрихкодов.АдресЭП)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаШтрихкодыБазы(ФильтрПоискаПоШтрихкоду = Неопределено) Экспорт
	
	Если ФильтрПоискаПоШтрихкоду = Неопределено Тогда
		ФильтрПоискаПоШтрихкоду = Новый Структура;
	КонецЕсли;
	
	ТекстОбъеденитьВсе = 
	"
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	МассивЗапросов = Новый Массив;
	ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаШтрихкоды(
		ФильтрПоискаПоШтрихкоду), МассивЗапросов);
	ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаШтрихкодыУпаковокТоваров(
		ФильтрПоискаПоШтрихкоду), МассивЗапросов);
	Если ФильтрПоискаПоШтрихкоду.Свойство("ЭтоКодМаркировки") Тогда
		ТекстЗапроса = СтрСоединить(МассивЗапросов, ТекстОбъеденитьВсе);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Если ФильтрПоискаПоШтрихкоду.Свойство("ШаблонКодТовараSKU") Тогда
		ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаКодыSKU(
			ФильтрПоискаПоШтрихкоду), МассивЗапросов);
	КонецЕсли;
	
	ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаСерии(
		ФильтрПоискаПоШтрихкоду),  МассивЗапросов);
	ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаНоменклатурнойКлассификации(
		ФильтрПоискаПоШтрихкоду), МассивЗапросов);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		
		Если ФильтрПоискаПоШтрихкоду.Свойство("ШаблонНомерТелефона") ИЛИ ФильтрПоискаПоШтрихкоду.Количество() = 0 Тогда
			ДобавитьТекстЗапросаОбъектыПоТелефону(МассивЗапросов, ФильтрПоискаПоШтрихкоду);
		КонецЕсли;
		
		Если ФильтрПоискаПоШтрихкоду.Свойство("ШаблонАдресЭП") ИЛИ ФильтрПоискаПоШтрихкоду.Количество() = 0  Тогда
			ДобавитьТекстЗапросаОбъектыПоАдресуЭП(МассивЗапросов, ФильтрПоискаПоШтрихкоду);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ФильтрПоискаПоШтрихкоду.Свойство("ШаблонНомерТелефона") тогда
		ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаПоТелефону(
			ФильтрПоискаПоШтрихкоду), МассивЗапросов);
		Если ФильтрПоискаПоШтрихкоду.Свойство("ДобавлятьНеизвестнуюКИ") Тогда
			ДобавитьТекстЗапросаВМассив(ТекстЗапросаТелефонПоШаблону(), МассивЗапросов);
		КонецЕсли;
	КонецЕсли;
	
	Если ФильтрПоискаПоШтрихкоду.Свойство("ШаблонАдресЭП") тогда
		ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаПоАдресуЭП(
			ФильтрПоискаПоШтрихкоду), МассивЗапросов);
		Если ФильтрПоискаПоШтрихкоду.Свойство("ДобавлятьНеизвестнуюКИ") Тогда
			ДобавитьТекстЗапросаВМассив(ТекстЗапросаАдресЭППоШаблону(), МассивЗапросов);
		КонецЕсли;
	КонецЕсли;
		
	Если ФильтрПоискаПоШтрихкоду.Свойство("РегистрацияНовойКарты") Тогда 
		ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаШаблоныРегистрацииКарт(
			ФильтрПоискаПоШтрихкоду), МассивЗапросов);
	КонецЕсли;
	
	ДобавитьТекстЗапросаВМассив(РаботаСоШтрихкодамиПереопределяемый.ТекстЗапросаДополнительныеОбъекты(
		ФильтрПоискаПоШтрихкоду), МассивЗапросов);
	
	ТекстЗапроса = СтрСоединить(МассивЗапросов, ТекстОбъеденитьВсе);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВМассив(ТекстЗапроса, МассивЗапросов)
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
		МассивЗапросов.Добавить(ТекстЗапроса);
	КонецЕсли;
	
КонецПроцедуры

Функция ТипКонтактнойИнформацияПоШтрихкоду(Штрихкод)
	
	НомерТелефона = СокрЛП(Штрихкод);
	НомерТелефона = СтрЗаменить(НомерТелефона, "(", "");
	НомерТелефона = СтрЗаменить(НомерТелефона, ")", "");
	НомерТелефона = СтрЗаменить(НомерТелефона, "-", "");
	НомерТелефона = СтрЗаменить(НомерТелефона, " ", "");
	НомерТелефона = СтрЗаменить(НомерТелефона, "+", "");
	
	АдресЭП = Штрихкод;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &НомерТелефона ПОДОБНО &ШаблонНомераТелефона
	|			ТОГДА 1
	|		КОГДА &АдресЭП ПОДОБНО &ШаблонАдресаЭП
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВаринатДанных");
	Запрос.УстановитьПараметр("НомерТелефона",  НомерТелефона);
	Запрос.УстановитьПараметр("АдресЭП",  АдресЭП);
	Запрос.УстановитьПараметр("ШаблонНомераТелефона", НСтр("ru = '[7-8][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]'"));
	Запрос.УстановитьПараметр("ШаблонАдресаЭП", НСтр("ru = '%@%.%'"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		СтруктураКИ = Новый Структура;
		
		Если Выборка.ВаринатДанных = 1 тогда
			СтруктураКИ.Вставить("НомерТелефона", НомерТелефона);
		ИначеЕсли Выборка.ВаринатДанных = 2 тогда
			СтруктураКИ.Вставить("АдресЭП", АдресЭП);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураКИ;
	
КонецФункции

Функция ДанныеПоискаПоШтрихкоду(Штрихкод, Форма) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыСобытийПО") Тогда
		Если Форма.ПараметрыСобытийПО = Неопределено Тогда
			СтруктураДействий = Новый Структура;
		Иначе
			СтруктураДействий = Форма.ПараметрыСобытийПО;
		КонецЕсли;
	Иначе
		СтруктураДействий = Новый Структура;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ФильтрПоискаПоШтрихкоду") Тогда
		Если Форма.ФильтрПоискаПоШтрихкоду = Неопределено Тогда
			ФильтрПоискаПоШтрихкоду = РаботаСоШтрихкодамиПереопределяемый.ФильтрПоискаПоУмолчанию();
		Иначе
			ФильтрПоискаПоШтрихкоду = Форма.ФильтрПоискаПоШтрихкоду;
		КонецЕсли;
	Иначе
		ФильтрПоискаПоШтрихкоду = РаботаСоШтрихкодамиПереопределяемый.ФильтрПоискаПоУмолчанию();
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	ШтрихкодПоиска = Штрихкод;
	ШтрихкодМарки = Штрихкод;
	НомерТелефона = "";
	АдресЭП = "";
	КИПоШтрихкоду = ТипКонтактнойИнформацияПоШтрихкоду(Штрихкод);
	Если КИПоШтрихкоду.Свойство("НомерТелефона", НомерТелефона) Тогда
		ФильтрПоискаПоШтрихкоду.Вставить("ШаблонНомерТелефона");
	КонецЕсли;
	Если КИПоШтрихкоду.Свойство("АдресЭП", АдресЭП) Тогда
		ФильтрПоискаПоШтрихкоду.Вставить("ШаблонАдресЭП");
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("РегистрацияНовойКарты") Тогда
		ФильтрПоискаПоШтрихкоду.Вставить("РегистрацияНовойКарты");
	КонецЕсли;
	
	Если РаботаСоШтрихкодамиПереопределяемый.ДобавлятьНеизвестнуюКИ(Форма) Тогда
		ФильтрПоискаПоШтрихкоду.Вставить("ДобавлятьНеизвестнуюКИ");
	КонецЕсли;
	
	Если ФильтрПоискаПоШтрихкоду.Свойство("Номенклатура") Тогда
		Если НЕ ФильтрПоискаПоШтрихкоду.Свойство("ШаблонНомерТелефона")
			И НЕ ФильтрПоискаПоШтрихкоду.Свойство("ШаблонАдресЭП") Тогда
			
			КодИКоличествоВесовогоТовара = РаботаСоШтрихкодамиПереопределяемый.КодИКоличествоВесовогоТовара(Штрихкод);
			
			Если НЕ КодИКоличествоВесовогоТовара = Неопределено Тогда
				ФильтрПоискаПоШтрихкоду.Вставить("ШаблонКодТовараSKU");
				Запрос.УстановитьПараметр("КодТовара", КодИКоличествоВесовогоТовара.КодТовара);
				Запрос.УстановитьПараметр("КоличествоТовара", КодИКоличествоВесовогоТовара.КоличествоТовара);
			КонецЕсли;
			
			Если НЕ ФильтрПоискаПоШтрихкоду.Свойство("ШаблонКодТовараSKU") Тогда
				ДанныеМаркировки = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьШтриховойКодТовара(Штрихкод);
				Если ЭтоКодМаркировки(ДанныеМаркировки) Тогда
					Если НЕ ДанныеМаркировки.EAN = Неопределено Тогда
						ШтрихкодПоиска = ДанныеМаркировки.EAN;
						ШтрихкодМарки = ДанныеМаркировки.EAN;
					КонецЕсли;
					ФильтрПоискаПоШтрихкоду.Вставить("ЭтоКодМаркировки");
					
					СимволGS1 = МенеджерОборудованияМаркировкаКлиентСервер.РазделительGS1();
					Разделитель = МенеджерОборудованияМаркировкаКлиентСервер.ЭкранированныйСимволGS1();
					КодМаркировкиОригинал = СтрЗаменить(Штрихкод, Разделитель, СимволGS1);
					ДанныеРазбораКода = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(КодМаркировкиОригинал);
					Если НЕ ДанныеРазбораКода = Неопределено Тогда
						ШтрихкодМарки = ДанныеРазбораКода.НормализованныйКодМаркировки;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапросаШтрихкодыБазы(ФильтрПоискаПоШтрихкоду);
	
	ТекстЗапросаВыборки = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ШтрихкодыВсе.Штрихкод КАК Штрихкод,
	|	ШтрихкодыВсе.Владелец КАК Владелец,
	|	ШтрихкодыВсе.Характеристика КАК Характеристика,
	|	ШтрихкодыВсе.Упаковка КАК Упаковка,
	|	ШтрихкодыВсе.Серия КАК СерияНоменклатуры,
	|	ШтрихкодыВсе.Партия КАК Партия,
	|	ТИПЗНАЧЕНИЯ(ШтрихкодыВсе.Владелец) КАК ТипОбъекта,
	|	ШтрихкодыВсе.ШаблонШтрихкода КАК ШаблонШтрихкода,
	|	0 КАК НомерСтроки,
	|	ШтрихкодыВсе.Количество КАК Количество
	|ИЗ
	|	ШтрихкодыВсе КАК ШтрихкодыВсе";
	
	ТекстЗапроса = ТекстЗапроса +
	"
	|; 
	|////////////////////////////////////////////////////////////////////////////////
	|" + ТекстЗапросаВыборки;
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Штрихкод", ШтрихкодПоиска);
	Запрос.УстановитьПараметр("ШтрихкодМарки", ШтрихкодМарки);
	Запрос.УстановитьПараметр("ДлинаШтрихкода", СтрДлина(ШтрихкодПоиска));
	Запрос.УстановитьПараметр("НомерТелефона", НомерТелефона);
	Запрос.УстановитьПараметр("АдресЭП", АдресЭП);
	
	РаботаСоШтрихкодамиПереопределяемый.ДополнитьПараметрыЗапросаПоиска(Запрос, Форма, ШтрихкодПоиска);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НеизвестныеДанныеПО = Истина;
	
	СтруктураПараметров = СтруктураДанныхПоиска();
	ДополнитьСтруктуруРезультатамиПоиска(СтруктураПараметров, Выборка, Штрихкод, "", НеизвестныеДанныеПО);
	
	РаботаСоШтрихкодамиПереопределяемый.СкорректироватьСтруктуруРезультата(
			СтруктураПараметров,
			Штрихкод,
			СтруктураДействий,
			НеизвестныеДанныеПО,
			Истина);
	
	НайденоОбъектов = СтруктураПараметров.ЗначенияПоиска.Количество();
	Если НайденоОбъектов > 1 тогда
		СтруктураПараметров.Вставить("ИспользоватьНовыйАлгоритмПоискаПоШтрихкоду");
	КонецЕсли;
	
	СтруктураПараметров.НеизвестныеДанныеПО = НеизвестныеДанныеПО;
	
	Позиция = НайтиНедопустимыеСимволыXML(Штрихкод);
	Если Позиция > 0 Тогда
		Штрихкод = Лев(Штрихкод, Позиция - 1);
	КонецЕсли;
	
	СтруктураПараметров.ДанныеПО = Штрихкод;
	СтруктураПараметров.ТипДанныхПО = "Штрихкод";
	
	Возврат СтруктураПараметров;
	
Конецфункции

Процедура ДополнитьСтруктуруРезультатамиПоиска(
	СтруктураПараметров,
	Выборка,
	Знач Штрихкод = "",
	МагнитныйКод = "",
	НеизвестныеДанныеПО = Ложь)
	
	Если Не ПустаяСтрока(Штрихкод) Тогда
		Позиция = НайтиНедопустимыеСимволыXML(Штрихкод);
		Если Позиция > 0 Тогда
			Штрихкод = Лев(Штрихкод, Позиция - 1);
		КонецЕсли;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		НеизвестныеДанныеПО = Ложь;
		
		СтруктураШтрихкода = НачатьСтруктуруРезультатовПоиска(СтруктураПараметров, Выборка, Штрихкод, МагнитныйКод);
		
		ЗаполнитьЗначенияСвойств(СтруктураШтрихкода, Выборка, , "Штрихкод");
		
		ДополнительныеДанные = Новый Структура;
		РаботаСоШтрихкодамиПереопределяемый.ЗаполнитьДополнительныеДанныеОбъектовВыборки(ДополнительныеДанные, Выборка);
		СтруктураШтрихкода.Вставить("ДополнительныеДанные", ДополнительныеДанные);
		
		Для Каждого ДополнительныйПараметр Из ДополнительныеДанные Цикл
			СтруктураШтрихкода.Вставить(ДополнительныйПараметр.Ключ, ДополнительныйПараметр.Значение);
		КонецЦикла;
		
		СтруктураПараметров.ЗначенияПоиска.Добавить(СтруктураШтрихкода);
		
	КонецЦикла;
	
КонецПроцедуры

Функция НачатьСтруктуруРезультатовПоиска(
	СтруктураПараметров,
	Выборка,
	Штрихкод,
	МагнитныйКод)
	
	СтруктураШтрихкода = Новый Структура;
	Если ЗначениеЗаполнено(СтруктураПараметров.ДанныеПО) Тогда
		СтруктураШтрихкода.Вставить("ДанныеПО", СтруктураПараметров.ДанныеПО);
	ИначеЕсли ЗначениеЗаполнено(Штрихкод) Тогда
		СтруктураШтрихкода.Вставить("ДанныеПО", Штрихкод);
	Иначе
		СтруктураШтрихкода.Вставить("ДанныеПО", МагнитныйКод);
	КонецЕсли;
	СтруктураШтрихкода.Вставить("НомерСтрокиЗагрузки", Выборка.НомерСтроки);
	СтруктураШтрихкода.Вставить("Штрихкод", Штрихкод);
	СтруктураШтрихкода.Вставить("МагнитныйКод", МагнитныйКод);
	СтруктураШтрихкода.Вставить("Количество", Выборка.Количество);
	
	СтруктураШтрихкода.Вставить("Владелец");
	СтруктураШтрихкода.Вставить("ТипОбъекта");
	СтруктураШтрихкода.Вставить("ШаблонШтрихкода");
	СтруктураШтрихкода.Вставить("Характеристика");
	СтруктураШтрихкода.Вставить("Упаковка");
	СтруктураШтрихкода.Вставить("СерияНоменклатуры");

	
	Возврат СтруктураШтрихкода;
	
КонецФункции

// Формирует предопределенную структуру,
// Которая используется при поиске по ШК.
//
// Возвращаемое значение:
//  Структура - структура данных поиска.
//
Функция СтруктураДанныхПоиска()
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("ЗначенияПоиска", Новый Массив);
	СтруктураПараметров.Вставить("НеизвестныеДанныеПО", Истина);
	СтруктураПараметров.Вставить("ДанныеПО", "");
	СтруктураПараметров.Вставить("ТипДанныхПО", "Штрихкод");
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция ЭтоКодМаркировки(ДанныеМаркировки)
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеМаркировки, "ТипИдентификатораТовара") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТипИдентификатораТовара = ДанныеМаркировки.ТипИдентификатораТовара;
	
	ТипыИдентификаторов = Перечисления.ТипыИдентификаторовТовараККТ;
	
	Возврат (ТипИдентификатораТовара = ТипыИдентификаторов.ИзделияИзНатуральногоМеха 
		ИЛИ ТипИдентификатораТовара = ТипыИдентификаторов.КодТовараВФорматеDataMatrixGS1
		ИЛИ ТипИдентификатораТовара = ТипыИдентификаторов.КодТовараВФорматеЕГАИС2
		ИЛИ ТипИдентификатораТовара = ТипыИдентификаторов.КодТовараВФорматеЕГАИС3);
	
КонецФункции

#КонецОбласти
