
Процедура Инициализация() Экспорт
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	КлючСлужебногоОбсуждения = ПолучитьКлючСлужебногоОбсужденияСотрудникаИнтернетМагазина();
	
	Для Каждого ПользовательИБ ИЗ ПользователиИнформационнойБазы.ПолучитьПользователей() Цикл
		Попытка
			ИдентификаторПользователяСВ = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(ПользовательИБ.УникальныйИдентификатор);
			ПользовательСВ = СистемаВзаимодействия.ПолучитьПользователя(ИдентификаторПользователяСВ);
		Исключение
			ПользовательСВ = СистемаВзаимодействия.СоздатьПользователя(ПользовательИБ);
			ПользовательСВ.Записать();
		КонецПопытки;
		
		Если ПользовательИБ.Роли.Содержит(Метаданные.Роли.ДобавлениеИзменениеЗаказовПокупателей) 
			Или ПользовательИБ.Роли.Содержит(Метаданные.Роли.ПолныеПрава) Тогда
			СлужебноеОбсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(КлючСлужебногоОбсуждения);
			
			Если СлужебноеОбсуждение = Неопределено Тогда
				СлужебноеОбсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
				СлужебноеОбсуждение.Отображаемое = Ложь;
				СлужебноеОбсуждение.Ключ = КлючСлужебногоОбсуждения;
			КонецЕсли;
			
			Если НЕ СлужебноеОбсуждение.Участники.Содержит(ПользовательСВ.Идентификатор) Тогда
				СлужебноеОбсуждение.Участники.Добавить(ПользовательСВ.Идентификатор);
			КонецЕсли;
			
			СлужебноеОбсуждение.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьКлючСлужебногоОбсужденияСотрудникаИнтернетМагазина() Экспорт
	
	Возврат "Работа с интернет-заказами";
	
КонецФункции

Функция ЭтаПодпискаДляСотрудникаИнтернетМагазина() Экспорт 
	
	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	Если ПользовательИБ.Роли.Содержит(Метаданные.Роли.ДобавлениеИзменениеЗаказовПокупателей) 
		Или ПользовательИБ.Роли.Содержит(Метаданные.Роли.ПолныеПрава) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ОтправкаУведомленийСотрудникамИнтернетМагазина(КлючСлужебногоОбсуждения, Заказ) Экспорт
	
	СлужебноеОбсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(КлючСлужебногоОбсуждения);
	
	СтруктураДанных = Новый Структура();
	СтруктураДанных.Вставить("Текст", "Новый интернет-заказ");
	СтруктураДанных.Вставить("Заказ", Заказ);
	
	СообщениеКлиенту = СистемаВзаимодействия.СоздатьСообщение(СлужебноеОбсуждение.Идентификатор);
	СообщениеКлиенту.Данные = СтруктураДанных;
	
	СообщениеКлиенту.Записать();
	
КонецПроцедуры


