
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ВидОперации = Перечисления.ds_ВидыОперацийУстановкаКатегорийНоменклатуры.Снятие Тогда 
		Категория = Справочники.ds_КатегорииНоменклатуры.ПустаяСсылка();
		ДатаОкончания = Дата(1, 1, 1);

		ТЗТовары = Товары.Выгрузить();
		ТЗТовары.ЗаполнитьЗначения(0, "Цена");
		Товары.Загрузить(ТЗТовары);
	КонецЕсли;	
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
    
    ВремяНачалаЗамера = ОценкаПроизводительности.НачатьЗамерВремени();

	Ценообразование.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	Документы.ds_УстановкаКатегорийНоменклатуры.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);

	Ценообразование.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ОтразитьЦеныНоменклатуры(ДополнительныеСвойства, Движения, Отказ);

	Ценообразование.ЗаписатьНаборыЗаписей(ЭтотОбъект);
    
    ОценкаПроизводительности.ЗакончитьЗамерВремени("УстановкаКатегорийНоменклатурыПроведение",ВремяНачалаЗамера,Товары.Количество(), Нстр("ru='Вес по табличной части ""Товары""'"));

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	Ценообразование.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	Ценообразование.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	Ценообразование.ЗаписатьНаборыЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ОтразитьЦеныНоменклатуры(ДополнительныеСвойства, Движения, Отказ)
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЦеныНоменклатуры;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияЦеныНоменклатуры            = Движения.ds_КатегорииНоменклатурыПлан;
	ДвиженияЦеныНоменклатуры.Записывать = Истина;
	
	ДвиженияЦеныНоменклатуры.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидОперации = Перечисления.ds_ВидыОперацийУстановкаКатегорийНоменклатуры.Снятие Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Категория");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Цена");
	КонецЕсли;	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если ЗначениеЗаполнено(ДатаОкончания) И ДатаНачала > ДатаОкончания Тогда	
		ОбщегоНазначения.СообщитьОбОшибке("Дата начала действия акции не может быть больше даты окончания!", Отказ);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
