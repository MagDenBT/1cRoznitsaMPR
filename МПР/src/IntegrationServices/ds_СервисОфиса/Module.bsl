
Процедура СетьМагазинов_ВОфисОбработкаПолученияСообщения(Сообщение, Отказ)
	
	СсылкаНаОбъект = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию("ExchangeMPR", Истина);
	ИмяОбработки = ВнешниеОбработки.Подключить(ПолучитьНавигационнуюСсылку(СсылкаНаОбъект, "ХранилищеОбработки"),, Ложь, ОбщегоНазначения.ОписаниеЗащитыБезПредупреждений());
	Обработка = ВнешниеОбработки.Создать(ИмяОбработки);
			
	РазмерСообщения = Сообщение.Параметры.Получить("РазмерСообщения"); 
	Если РазмерСообщения <> Неопределено Тогда 
		РазмерБуфера = Число(РазмерСообщения); 
	Иначе    
		РазмерБуфера = 1024; 
	КонецЕсли; 
	
	Тело = Новый БуферДвоичныхДанных(0);
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера); 
	
	Поток = Сообщение.ПолучитьТелоКакПоток(); 
	
	Пока Истина Цикл   
		Попытка
			Прочитано = Поток.Прочитать(Буфер, 0, РазмерБуфера);
		Исключение
			Возврат;
		КонецПопытки;
		
		Если Прочитано > 0 Тогда
			Тело = Тело.Соединить(Буфер); 
		КонецЕсли;
		Если Прочитано < РазмерБуфера Тогда  
			Прервать;  
		КонецЕсли; 
	КонецЦикла; 
	
	СтрокаСообщения = ПолучитьСтрокуИзБуфераДвоичныхДанных(Тело); 
	
	ЧтениеJson = Новый ЧтениеJSON;
	ЧтениеJson.УстановитьСтроку(СтрокаСообщения);
	Данные = ПрочитатьJSON(ЧтениеJson);
	
	Отправитель = ПланыОбмена.ds_ОбменСОфисом.НайтиПоКоду(Данные.Отправитель);
	Если Отправитель.Пустая() Тогда
		Возврат;
	КонецЕсли;	
				
	ТипСообщения = Сообщение.Параметры.Получить("ТипСообщения");  
	Если ТипСообщения = "Обмен" Тогда
		Если Не Отправитель.ПропускатьСообщения Тогда 
			Обработка.ЗагрузитьИзмененияОтМагазина(Данные);
		КонецЕсли;	
		
		Узел = Отправитель.ПолучитьОбъект();
		Узел.Версия = Данные.Версия;
		Узел.НомерПринятого = Число(Данные.НомерОтправленного);
		Узел.ДатаОбмена = ТекущаяДата();
		Узел.Записать();
	ИначеЕсли ТипСообщения = "СверкаОстатков" Тогда		
		Если Не Отправитель.ПропускатьСообщения Тогда
			НомерСообщения = Сообщение.Параметры.Получить("НомерСообщения");
			Если НомерСообщения = "2" Тогда 
				Обработка.ПрочитатьСообщение2ВОфисе(Данные);
			ИначеЕсли НомерСообщения = "4" Тогда 
				Обработка.ПрочитатьСообщение4ВОфисе(Данные);
				
				Узел = Отправитель.ПолучитьОбъект();
				Узел.ДатаАктивнойСверки = Дата(1, 1, 1);
				Узел.Записать();								
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры
